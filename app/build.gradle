apply plugin: 'com.android.application'

// 读取蒲公英配置文件
def pgyerAppId = ""
def currentTime = new Date().format('yyyyMMddhhmmss')
def pgyPropertiesFileName = "pgyer.properties";

if (!(new File(pgyPropertiesFileName).exists())) {
    println("WARNING：请先拷贝pgyer.properties.sample 为 pgyer.properties，并且配置PGYER_APPID")
} else {
    Properties props = new Properties()
    props.load(new FileInputStream(file(pgyPropertiesFileName)))
    pgyerAppId = props["PGYER_APPID"]
}



android {
    compileSdkVersion 23
    //noinspection GradleDependency
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId "com.xiaoxuetu.shield"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0.0.${currentTime}-beta"
    }


    buildTypes {

        release {
            minifyEnabled true
            zipAlignEnabled true
            //移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    "pgyerAppId": pgyerAppId,
                    "appName": "@string/app_name"
            ]

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def file = output.outputFile

                    if (file != null && file.name.endsWith('.apk') && !file.name.contains("debug")) {

                        //生成的文件名：TPLinkManager+阶段名称+版本号+生成时间.apk
                        def fileName = "TPLinkManager-${defaultConfig.versionName}.apk"
                        output.outputFile = new File(file.parent,fileName)
                    }

                }
            }
        }

        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [
                    "pgyerAppId": pgyerAppId,
                    "appName": "@string/app_name_debug"
            ]
        }

    }

    // 暂时忽略lint错误
    lintOptions {
        abortOnError false
    }
}



dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.2.1'
    //noinspection GradleDependency
    compile 'com.android.support:appcompat-v7:23+'
    compile 'in.srain.cube:ultra-ptr:1.0.11'
    compile 'com.pgyersdk:sdk:2.4.5'
    compile project(path: ':route_api')
}
